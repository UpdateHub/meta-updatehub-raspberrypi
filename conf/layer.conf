# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "updatehub-raspberrypi"
BBFILE_PATTERN_updatehub-raspberrypi = "^${LAYERDIR}/"
BBFILE_PRIORITY_updatehub-raspberrypi = "6"

LAYERSERIES_COMPAT_updatehub-raspberrypi = "dunfell"
LAYERDEPENDS_updatehub-raspberrypi = "updatehub raspberrypi"

###
# Global setting to use UpadateHub with meta-rasberrypi layer
#
# Setting to use wic image
WKS_FILE_DEPENDS_updatehub-rpi += "${IMAGE_BOOTLOADER} virtual/bootloader"
IMAGE_FSTYPES_updatehub-rpi ??= "tar.lzma wic.bmap wic.gz"
WKS_FILES_updatehub-rpi ??= "${MACHINE}.wks updatehub.rpi.wks"
WKS_SEARCH_PATH_updatehub-rpi ??= "${THISDIR}:${@':'.join('%s/wic' % p for p in '${BBPATH}'.split(':'))}:${@':'.join('%s/scripts/lib/wic/canned-wks' % l for l in '${BBPATH}:${COREBASE}'.split(':'))}"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND_updatehub-rpi ??= "u-boot"
UPDATEHUB_DEVICE_IDENTITY_updatehub-rpi ??= "cpuinfo-serial"
UPDATEHUB_FILESYSTEM_SUPPORT_updatehub-rpi ??= "vfat ext4"
UPDATEHUB_IMAGE_TYPE_updatehub-rpi ??= "active/inactive"
UPDATEHUB_INSTALL_MODE_updatehub-rpi ??= "copy tarball"

PREFERRED_PROVIDER_u-boot-fw-utils_updatehub-rpi ??= "u-boot-updatehub-fw-utils"
PREFERRED_PROVIDER_u-boot_updatehub-rpi ??= "u-boot-updatehub"
PREFERRED_PROVIDER_virtual/bootloader_updatehub-rpi ??= "u-boot-updatehub"
RPI_USE_U_BOOT_updatehub-rpi = "1"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_updatehub-rpi += " \
    kernel-image \
    kernel-devicetree \
"

# Do not change fstab file
WIC_CREATE_EXTRA_ARGS_updatehub-rpi ??= "--no-fstab-update"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi3 machine
MACHINEOVERRIDES_prepend_raspberrypi3 = "updatehub-rpi:"

IMAGE_BOOT_FILES_updatehub-rpi_raspberrypi3 ??= " \
    bcm2835-bootfiles/bootcode.bin;bootcode.bin \
    bcm2835-bootfiles/config.txt;config.txt \
    bcm2835-bootfiles/start.elf;start.elf \
    bcm2835-bootfiles/start_cd.elf;start_cd.elf \
    bcm2835-bootfiles/start_db.elf;start_db.elf \
    bcm2835-bootfiles/start_x.elf;start_x.elf \
    bcm2835-bootfiles/fixup.dat;fixup.dat \
    bcm2835-bootfiles/fixup_cd.dat;fixup_cd.dat \
    bcm2835-bootfiles/fixup_db.dat;fixup_db.dat \
    bcm2835-bootfiles/fixup_x.dat;fixup_x.dat \
    bcm2710-rpi-3-b.dtb \
    bcm2710-rpi-3-b-plus.dtb \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi3-64 machine
MACHINEOVERRIDES_prepend_raspberrypi3-64 = "updatehub-rpi:"

KERNEL_IMAGETYPE_UBOOT_updatehub-rpi_raspberrypi3-64 ??= "Image"
UHUPKG_FILES_updatehub-rpi_raspberrypi3-64 ??= "raspberrypi3.uhupkg"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi4 machine
MACHINEOVERRIDES_prepend_raspberrypi4 = "updatehub-rpi:"

IMAGE_BOOT_FILES_updatehub-rpi_raspberrypi4 ??= " \
    bcm2835-bootfiles/bootcode.bin;bootcode.bin \
    bcm2835-bootfiles/config.txt;config.txt \
    bcm2835-bootfiles/fixup4.dat;fixup4.dat \
    bcm2835-bootfiles/fixup4cd.dat;fixup4cd.dat \
    bcm2835-bootfiles/fixup4db.dat;fixup4db.dat \
    bcm2835-bootfiles/fixup4x.dat;fixup4x.dat \
    bcm2835-bootfiles/start4.elf;start4.elf \
    bcm2835-bootfiles/start4cd.elf;start4cd.elf \
    bcm2835-bootfiles/start4db.elf;start4db.elf \
    bcm2835-bootfiles/start4x.elf;start4x.elf \
    bcm2711-rpi-4-b.dtb \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi4-64 machine
MACHINEOVERRIDES_prepend_raspberrypi4-64 = "updatehub-rpi:"

KERNEL_IMAGETYPE_UBOOT_updatehub-rpi_raspberrypi4-64 ??= "Image"
UHUPKG_FILES_updatehub-rpi_raspberrypi4-64 ??= "raspberrypi4.wks"

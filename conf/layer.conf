# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "updatehub-raspberrypi"
BBFILE_PATTERN_updatehub-raspberrypi = "^${LAYERDIR}/"
BBFILE_PRIORITY_updatehub-raspberrypi = "6"

LAYERSERIES_COMPAT_updatehub-raspberrypi = "hardknott"
LAYERDEPENDS_updatehub-raspberrypi = "updatehub raspberrypi"

###
# Global setting to use UpadateHub with meta-rasberrypi layer
#
# Setting to use wic image
WKS_FILE_DEPENDS_updatehub-rpi += "virtual/bootloader"
IMAGE_FSTYPES_updatehub-rpi ??= "tar.lzma wic.bmap wic.gz"
WKS_FILES_updatehub-rpi ??= "${MACHINE}.wks updatehub.rpi.wks"
WKS_SEARCH_PATH_updatehub-rpi ??= "${THISDIR}:${@':'.join('%s/wic' % p for p in '${BBPATH}'.split(':'))}:${@':'.join('%s/scripts/lib/wic/canned-wks' % l for l in '${BBPATH}:${COREBASE}'.split(':'))}"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND_updatehub-rpi ??= "u-boot"
UPDATEHUB_DEVICE_IDENTITY_updatehub-rpi ??= "cpuinfo-serial"
UPDATEHUB_FILESYSTEM_SUPPORT_updatehub-rpi ??= "vfat ext4"
UPDATEHUB_IMAGE_TYPE_updatehub-rpi ??= "active/inactive"
UPDATEHUB_INSTALL_MODE_updatehub-rpi ??= "copy tarball"

PREFERRED_PROVIDER_u-boot-fw-utils_updatehub-rpi ??= "libubootenv"
PREFERRED_PROVIDER_u-boot_updatehub-rpi ??= "u-boot"
PREFERRED_PROVIDER_virtual/bootloader_updatehub-rpi ??= "u-boot"
PREFERRED_PROVIDER_u-boot-tools-native ??= "u-boot-tools-native"
PREFERRED_PROVIDER_u-boot-mkimage-native ??= "u-boot-tools-native"
PREFERRED_PROVIDER_u-boot-mkimage ??= "u-boot-tools"
PREFERRED_PROVIDER_nativesdk-u-boot-mkimage ??= "nativesdk-u-boot-tools"
PREFERRED_PROVIDER_u-boot-default-script_updatehub-rpi ??= "u-boot-updatehub-script"
UPDATEHUB_RUNTIME_PACKAGES_append_updatehub-rpi = " u-boot-default-script u-boot-default-env"
RPI_USE_U_BOOT_updatehub-rpi = "1"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_updatehub-rpi += " \
    kernel-image \
    kernel-devicetree \
"

# Do not change fstab file
WIC_CREATE_EXTRA_ARGS_updatehub-rpi ??= "--no-fstab-update"

# UpdateHub Bootscript variables for raspberrypi3/64 and raspberrypi4/64 machine
UPDATEHUB_BOOTSCRIPT_LOAD_A_updatehub-rpi ??= "\
load mmc \${devnum}:2 \${kernel_addr_r} /boot/${KERNEL_IMAGETYPE}; fdt addr \${fdt_addr}; \
fdt get value bootargs_dtb /chosen bootargs \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B_updatehub-rpi ??= "\
load mmc \${devnum}:3 \${kernel_addr_r} /boot/${KERNEL_IMAGETYPE}; fdt addr \${fdt_addr}; \
fdt get value bootargs_dtb /chosen bootargs \
"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A_updatehub-rpi ??= "part uuid mmc \${devnum}:2 uuid"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B_updatehub-rpi ??= "part uuid mmc \${devnum}:3 uuid"
UPDATEHUB_BOOTSCRIPT_BOOTARGS_updatehub-rpi ??= "\${bootargs_dtb} root=PARTUUID=\${uuid} rw kgdboc=serial0"
UPDATEHUB_BOOTSCRIPT_BOOTCMD_updatehub-rpi ??= "${KERNEL_BOOTCMD} \${kernel_addr_r} - \${fdt_addr}"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi3 machine
MACHINEOVERRIDES_prepend_raspberrypi3 = "updatehub-rpi:"

IMAGE_BOOT_FILES_updatehub-rpi_raspberrypi3 ??= " \
    ${BOOTFILES_DIR_NAME}/bootcode.bin;bootcode.bin \
    ${BOOTFILES_DIR_NAME}/config.txt;config.txt \
    ${BOOTFILES_DIR_NAME}/start.elf;start.elf \
    ${BOOTFILES_DIR_NAME}/start_cd.elf;start_cd.elf \
    ${BOOTFILES_DIR_NAME}/start_db.elf;start_db.elf \
    ${BOOTFILES_DIR_NAME}/start_x.elf;start_x.elf \
    ${BOOTFILES_DIR_NAME}/fixup.dat;fixup.dat \
    ${BOOTFILES_DIR_NAME}/fixup_cd.dat;fixup_cd.dat \
    ${BOOTFILES_DIR_NAME}/fixup_db.dat;fixup_db.dat \
    ${BOOTFILES_DIR_NAME}/fixup_x.dat;fixup_x.dat \
    bcm2710-rpi-3-b.dtb \
    bcm2710-rpi-3-b-plus.dtb \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
    boot.scr-${MACHINE};boot.scr \
"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi3-64 machine
MACHINEOVERRIDES_prepend_raspberrypi3-64 = "updatehub-rpi:"

KERNEL_IMAGETYPE_UBOOT_updatehub-rpi_raspberrypi3-64 ??= "Image"
UHUPKG_FILES_updatehub-rpi_raspberrypi3-64 ??= "raspberrypi3.uhupkg"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi4 machine
MACHINEOVERRIDES_prepend_raspberrypi4 = "updatehub-rpi:"

IMAGE_BOOT_FILES_updatehub-rpi_raspberrypi4 ??= " \
    ${BOOTFILES_DIR_NAME}/bootcode.bin;bootcode.bin \
    ${BOOTFILES_DIR_NAME}/config.txt;config.txt \
    ${BOOTFILES_DIR_NAME}/fixup4.dat;fixup4.dat \
    ${BOOTFILES_DIR_NAME}/fixup4cd.dat;fixup4cd.dat \
    ${BOOTFILES_DIR_NAME}/fixup4db.dat;fixup4db.dat \
    ${BOOTFILES_DIR_NAME}/fixup4x.dat;fixup4x.dat \
    ${BOOTFILES_DIR_NAME}/start4.elf;start4.elf \
    ${BOOTFILES_DIR_NAME}/start4cd.elf;start4cd.elf \
    ${BOOTFILES_DIR_NAME}/start4db.elf;start4db.elf \
    ${BOOTFILES_DIR_NAME}/start4x.elf;start4x.elf \
    bcm2711-rpi-4-b.dtb \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
    boot.scr-${MACHINE};boot.scr \
"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi4-64 machine
MACHINEOVERRIDES_prepend_raspberrypi4-64 = "updatehub-rpi:"

KERNEL_IMAGETYPE_UBOOT_updatehub-rpi_raspberrypi4-64 ??= "Image"
UHUPKG_FILES_updatehub-rpi_raspberrypi4-64 ??= "raspberrypi4.wks"

###
# Configuration for Raspberry Pi Devices
#
# UpdateHub settings for raspberrypi0-wifi machine
MACHINEOVERRIDES_prepend_raspberrypi0-wifi = "updatehub-rpi:"

IMAGE_BOOT_FILES_updatehub-rpi_raspberrypi0-wifi ??= " \
    ${BOOTFILES_DIR_NAME}/bootcode.bin;bootcode.bin \
    ${BOOTFILES_DIR_NAME}/config.txt;config.txt \
    ${BOOTFILES_DIR_NAME}/fixup.dat;fixup.dat \
    ${BOOTFILES_DIR_NAME}/fixup_cd.dat;fixup_cd.dat \
    ${BOOTFILES_DIR_NAME}/fixup_db.dat;fixup_db.dat \
    ${BOOTFILES_DIR_NAME}/fixup_x.dat;fixup_x.dat \
    ${BOOTFILES_DIR_NAME}/start.elf;start.elf \
    ${BOOTFILES_DIR_NAME}/start_cd.elf;start_cd.elf \
    ${BOOTFILES_DIR_NAME}/start_db.elf;start_db.elf \
    ${BOOTFILES_DIR_NAME}/start_x.elf;start_x.elf \
    bcm2708-rpi-zero-w.dtb \
    boot.scr-${MACHINE};boot.scr \
    u-boot.bin;${SDIMG_KERNELIMAGE} \
"
